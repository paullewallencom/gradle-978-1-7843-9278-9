repositories {
    maven {
        name = 'Company Maven Repository'

        url 'http://ourcompany.com/maven'

        // Check that properties mavenUsername
        // and mavenPassword are set when
        // we run the script.
        verifyProperty('mavenUsername')
        verifyProperty('mavenPassword')

        credentials {
            // Use project properties instead
            // of hard coded username and password.
            username mavenUsername
            password mavenPassword
        }
    }
}

/**
 * Helper method to check if project property
 * with the given name is set.
 * 
 * @param propertyName Name of the property to check
 * @throws GradleException When property is not set.
 */
void verifyProperty(final String propertyName) {
    if (!hasProperty(propertyName)) {
        throw new GradleException("Property $propertyName must be set")
    }
}